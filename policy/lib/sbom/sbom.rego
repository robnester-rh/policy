package lib.sbom

import data.lib
import data.lib.rule_data

import data.lib.image
import data.lib.json as j
import data.lib.oci
import data.lib.sigstore
import data.lib.tekton
import rego.v1

# SBOM discovery is additive: every supported source is checked, and all results
# associated with the image being evaluated are returned. Sources fall into
# three groups:
#
#   1. signed SBOM attestations attached to the image;
#   2. SBOM blobs recorded by build tasks in a PipelineRun attestation; and
#   3. signed artifacts attached to the image through OCI referrers or legacy
#      tag-based references.
#
# Each discovered document is classified as CycloneDX or SPDX. The public rules
# expose arrays for callers, while the private set rules deduplicate documents
# found through more than one source.

_sbom_artifact_types := {
	"application/spdx+json",
	"application/vnd.cyclonedx+json",
}

all_sboms := array.concat(cyclonedx_sboms, spdx_sboms)

cyclonedx_sboms := [s | some s in _cyclonedx_sboms]

spdx_sboms := [s | some s in _spdx_sboms]

# Build the result for each format in two stages. The *_without_image rules
# collect SBOMs already present in attestations supplied to policy evaluation.
# The *_from_image rules perform additional registry discovery for the image.
_cyclonedx_sboms := _cyclonedx_sboms_without_image | _cyclonedx_sboms_from_image
_cyclonedx_sboms_without_image := _cyclonedx_sboms_from_attestations | _cyclonedx_sboms_from_pipelinerun

_spdx_sboms := _spdx_sboms_without_image | _spdx_sboms_from_image
_spdx_sboms_without_image := _spdx_sboms_from_attestations | _spdx_sboms_from_pipelinerun

# Signed SBOM attestations carry the SBOM directly as the in-toto predicate.
# Verification is shared by both format-specific rules below; predicateType
# determines which result set receives each document.
_cyclonedx_sboms_from_attestations contains statement.predicate if {
	some att in _verified_sbom_attestations
	statement := att.statement

	# https://cyclonedx.org/specification/overview/#recognized-predicate-type
	statement.predicateType == "https://cyclonedx.org/bom"
}

_cyclonedx_sboms_from_pipelinerun contains sbom if {
	some sbom in _fetch_pipelinerun_sbom
	sbom.bomFormat == "CycloneDX"
}

_cyclonedx_sboms_from_image contains sbom if {
	some sbom in _fetch_sboms_from_image
	sbom.bomFormat == "CycloneDX"
}

_spdx_sboms_from_attestations contains statement.predicate if {
	some att in _verified_sbom_attestations
	statement := att.statement
	statement.predicateType == "https://spdx.dev/Document"
}

# Verify attached SBOM attestations once, using the named "sbom" signing
# identity, before either format-specific rule consumes them. With no configured
# identity this set remains empty, so attached attestations are excluded.
_verified_sbom_attestations contains attestation if {
	_sbom_signing_identity_configured
	verification := ec.sigstore.verify_attestation(input.image.ref, _sbom_signing_identity)
	object.get(verification, "success", false) == true
	some attestation in verification.attestations
}

_spdx_sboms_from_pipelinerun contains sbom if {
	some sbom in _fetch_pipelinerun_sbom
	sbom.SPDXID == "SPDXRef-DOCUMENT"
}

_spdx_sboms_from_image contains sbom if {
	some sbom in _fetch_sboms_from_image
	sbom.SPDXID == "SPDXRef-DOCUMENT"
}

# PipelineRun attestations are already part of the policy input. Their build
# tasks point to external SBOM blobs through SBOM_BLOB_URL, so resolve each blob
# after confirming that the task's IMAGE_DIGEST matches the evaluated image.
# This digest check is what selects the correct platform SBOM when one
# PipelineRun describes several platform images.
_fetch_pipelinerun_sbom contains sbom if {
	some attestation in lib.pipelinerun_attestations
	some task in tekton.build_tasks(attestation)

	# For multi-platform images, the same SLSA Provenance may describe all the platform specific
	# images. Each will have its own SBOM. Only select the SBOM for the image being evaluated.
	expected_image_digest := image.parse(input.image.ref).digest
	image_digest := tekton.task_result(task, "IMAGE_DIGEST")
	expected_image_digest == image_digest

	blob_ref := tekton.task_result(task, "SBOM_BLOB_URL")
	sbom := oci.parsed_blob(blob_ref)
}

# Registry discovery supports both the current OCI Referrers API and the legacy
# cosign tag convention. Both are evaluated and unioned; neither is a fallback
# for the other. The set union also deduplicates a document exposed both ways.
_fetch_sboms_from_image := _sboms_from_referrers | _sboms_from_tag_refs

# OCI referrers advertise their content type in artifactType. Verify each
# referrer first, keep only recognized SBOM media types, then parse its blob.
# With no configured "sbom" signing identity, this source yields no SBOMs.
_sboms_from_referrers contains sbom if {
	_sbom_signing_identity_configured
	some referrer in oci.verified_image_referrers(input.image.ref, _sbom_signing_identity)
	referrer.artifactType in _sbom_artifact_types
	sbom := oci.parsed_blob(referrer.ref)
}

# Legacy discovery derives image tag references and identifies SBOM artifacts by
# the .sbom suffix. As with referrers, only references verified with the named
# "sbom" signing identity are parsed; without that identity this source is empty.
_sboms_from_tag_refs contains sbom if {
	_sbom_signing_identity_configured
	some ref in oci.verified_image_tag_refs(input.image.ref, _sbom_signing_identity)
	endswith(ref, ".sbom")
	sbom := oci.parsed_blob_from_image(ref)
}

has_item(needle, haystack) if {
	needle_purl := ec.purl.parse(needle)

	some hay in haystack
	hay_purl := ec.purl.parse(hay.purl)

	needle_purl.type == hay_purl.type
	needle_purl.namespace == hay_purl.namespace
	needle_purl.name == hay_purl.name
	_matches_version(needle_purl.version, hay)

	not _excluded(needle_purl, object.get(hay, "exceptions", []))
} else := false

_excluded(purl, exceptions) if {
	matches := [exception |
		some exception in exceptions
		exception.subpath == purl.subpath
	]
	count(matches) > 0
}

_matches_version(version, matcher) if {
	matcher.format in {"semverv", "semver"}
	matcher.min != ""
	matcher.max != ""
	semver.compare(_to_semver(version), _to_semver(matcher.min)) != -1
	semver.compare(_to_semver(version), _to_semver(matcher.max)) != 1
} else if {
	matcher.format in {"semverv", "semver"}
	matcher.min != ""
	object.get(matcher, "max", "") == ""
	semver.compare(_to_semver(version), _to_semver(matcher.min)) != -1
} else if {
	matcher.format in {"semverv", "semver"}
	matcher.max != ""
	object.get(matcher, "min", "") == ""
	semver.compare(_to_semver(version), _to_semver(matcher.max)) != 1
} else := false

_to_semver(v) := trim_prefix(v, "v")

# get allowed pattens for given purl type, or empty list if not defined
purl_allowed_patterns(purl_type, allowed_rule_data) := patterns if {
	some allowed in allowed_rule_data
	purl_type == allowed.type
	patterns := allowed.patterns
} else := []

# see if any pattern matches given url
url_matches_any_pattern(url, patterns) if {
	some pattern in patterns
	regex.match(pattern, url)
}

image_ref_from_purl(raw_purl) := image_ref if {
	# Example purl:
	#   "pkg:oci/someapp@sha256:012abc?repository_url=someregistry.io/someorg/someapp"
	purl := ec.purl.parse(raw_purl)
	purl.type == "oci"

	# Todo maybe: We see "oci" in SBOMs produced by Konflux, but I think
	# other SPDX creators might reasonably use "pkg:docker/" in the purl.
	# purl.type in {"oci", "docker"}

	# Example image_digest: "sha256:012abc0000000000000000000000000000000000000000000000000000012abc"
	image_digest := purl.version

	some qualifier in purl.qualifiers
	qualifier.key == "repository_url"

	# Example repo_url: "someregistry.io/someorg/someapp"
	# It's probably the same as pkg.name, but let's use the value from the purl
	repo_url := qualifier.value

	# Put them together to make a pinned image_ref
	image_ref := sprintf("%s@%s", [repo_url, image_digest])
}

# Verify disallowed_packages is an array of objects
rule_data_errors contains error if {
	some e in j.validate_schema(rule_data.get(rule_data_packages_key), {
		"$schema": "http://json-schema.org/draft-07/schema#",
		"type": "array",
		"uniqueItems": true,
		"items": {
			"type": "object",
			"properties": {
				"purl": {"type": "string"},
				"format": {"enum": ["semver", "semverv"]},
				"min": {"type": "string"},
				"max": {"type": "string"},
				"exceptions": {
					"type": "array",
					"uniqueItems": true,
					"items": {
						"type": "object",
						"properties": {"subpath": {"type": "string"}},
					},
				},
			},
			"additionalProperties": false,
			"anyOf": [
				{"required": ["purl", "format", "min"]},
				{"required": ["purl", "format", "max"]},
			],
		},
	})
	error := {
		"message": sprintf("Rule data %s has unexpected format: %s", [rule_data_packages_key, e.message]),
		"severity": e.severity,
	}
}

# Verify each item in disallowed_packages has a parseable PURL
rule_data_errors contains error if {
	some index, pkg in rule_data.get(rule_data_packages_key)
	purl := pkg.purl
	not ec.purl.is_valid(purl)
	error := {
		"message": sprintf("Item at index %d in %s does not have a valid PURL: %q", [index, rule_data_packages_key, purl]),
		"severity": "failure",
	}
}

# Verify each item in disallowed_packages has a parseable min/max semver
rule_data_errors contains error if {
	some index, pkg in rule_data.get(rule_data_packages_key)
	pkg.format in {"semver", "semverv"}
	some attr in ["min", "max"]

	version := _to_semver(object.get(pkg, attr, ""))
	version != ""

	not semver.is_valid(version)

	error := {
		"message": sprintf(
			"Item at index %d in %s does not have a valid %s semver value: %q",
			[index, rule_data_packages_key, attr, version],
		),
		"severity": "failure",
	}
}

# Verify disallowed_attributes is an array of name value pairs
rule_data_errors contains error if {
	some e in j.validate_schema(rule_data.get(rule_data_attributes_key), {
		"$schema": "http://json-schema.org/draft-07/schema#",
		"type": "array",
		"uniqueItems": true,
		"items": {
			"type": "object",
			"properties": {
				"name": {"type": "string"},
				"value": {"type": "string"},
				"effective_on": {"type": "string", "format": "date-time"},
				"except_when": {
					"type": "array",
					"items": {
						"type": "object",
						"properties": {
							"purl_qualifier": {"type": "string"},
							"patterns": {
								"type": "array",
								"items": {"type": "string", "format": "regex"},
							},
						},
						"required": ["purl_qualifier", "patterns"],
						"additionalProperties": false,
					},
				},
			},
			"additionalProperties": false,
			"required": ["name"],
		},
	})
	error := {
		"message": sprintf("Rule data %s has unexpected format: %s", [rule_data_attributes_key, e.message]),
		"severity": e.severity,
	}
}

# Verify items in disallowed_attributes except_when have valid regular expressions.
rule_data_errors contains error if {
	some attr_index, attr in rule_data.get(rule_data_attributes_key)
	some ew in object.get(attr, "except_when", [])
	some pattern in ew.patterns
	not regex.is_valid(pattern)
	error := {
		"message": sprintf(
			"Item at index %d in %s has an invalid regular expression in except_when: %q",
			[attr_index, rule_data_attributes_key, pattern],
		),
		"severity": "failure",
	}
}

# Verify allowed_external_references is an array of type/url pairs
rule_data_errors contains error if {
	some e in j.validate_schema(rule_data.get(rule_data_allowed_external_references_key), {
		"$schema": "http://json-schema.org/draft-07/schema#",
		"type": "array",
		"uniqueItems": true,
		"items": {
			"type": "object",
			"properties": {
				"type": {"type": "string"},
				"url": {"type": "string"},
			},
			"additionalProperties": false,
			"required": ["type", "url"],
		},
	})
	error := {
		"message": sprintf("Rule data %s has unexpected format: %s", [rule_data_allowed_external_references_key, e.message]),
		"severity": e.severity,
	}
}

# Verify disallowed_external_references is an array of type/url pairs
rule_data_errors contains error if {
	some e in j.validate_schema(rule_data.get(rule_data_disallowed_external_references_key), {
		"$schema": "http://json-schema.org/draft-07/schema#",
		"type": "array",
		"uniqueItems": true,
		"items": {
			"type": "object",
			"properties": {
				"type": {"type": "string"},
				"url": {"type": "string"},
			},
			"additionalProperties": false,
			"required": ["type", "url"],
		},
	})

	error := {
		# regal ignore:line-length
		"message": sprintf("Rule data %s has unexpected format: %s", [rule_data_disallowed_external_references_key, e.message]),
		"severity": e.severity,
	}
}

# Verify allowed_package_sources is array of purl/regex list pairs
rule_data_errors contains error if {
	some e in j.validate_schema(
		rule_data.get(rule_data_allowed_package_sources_key),
		{
			"$schema": "http://json-schema.org/draft-07/schema#",
			"type": "array",
			"items": {
				"type": "object",
				"properties": {
					"type": {"type": "string"},
					"patterns": {
						"type": "array",
						"items": {
							"type": "string",
							"format": "regex",
						},
					},
				},
				"required": ["type", "patterns"],
				"additionalProperties": false,
			},
		},
	)

	error := {
		# regal ignore:line-length
		"message": sprintf("Rule data %s has unexpected format: %s", [rule_data_allowed_package_sources_key, e.message]),
		"severity": e.severity,
	}
}

# Verify proxy_enabled_purl_types is a list of unique strings.
rule_data_errors contains error if {
	some e in j.validate_schema(
		rule_data.get("proxy_enabled_purl_types"),
		{
			"$schema": "http://json-schema.org/draft-07/schema#",
			"type": "array",
			"items": {"type": "string"},
			"uniqueItems": true,
		},
	)
	error := {
		"message": sprintf("Rule data proxy_enabled_purl_types has unexpected format: %s", [e.message]),
		"severity": e.severity,
	}
}

# Verify vendored_purl_types is a list of unique strings.
rule_data_errors contains error if {
	some e in j.validate_schema(
		rule_data.get("vendored_purl_types"),
		{
			"$schema": "http://json-schema.org/draft-07/schema#",
			"type": "array",
			"items": {"type": "string"},
			"uniqueItems": true,
		},
	)
	error := {
		"message": sprintf("Rule data vendored_purl_types has unexpected format: %s", [e.message]),
		"severity": e.severity,
	}
}

# Verify allowed_proxy_url_patterns is an object mapping strings to arrays of strings.
rule_data_errors contains error if {
	some e in j.validate_schema(
		rule_data.get("allowed_proxy_url_patterns"),
		{
			"$schema": "http://json-schema.org/draft-07/schema#",
			"type": "object",
			"additionalProperties": {
				"type": "array",
				"items": {"type": "string"},
				"uniqueItems": true,
			},
		},
	)
	error := {
		"message": sprintf("Rule data allowed_proxy_url_patterns has unexpected format: %s", [e.message]),
		"severity": e.severity,
	}
}

# Verify items in allowed_proxy_url_patterns are valid regular expressions.
rule_data_errors contains error if {
	some purl_type, patterns in rule_data.get("allowed_proxy_url_patterns")
	some pattern in patterns
	not regex.is_valid(pattern)
	error := {
		"message": sprintf("%q is not a valid regular expression for PURL type %q", [pattern, purl_type]),
		"severity": "failure",
	}
}

# Warn when external_references URL patterns lack effective ^ anchoring.
rule_data_errors contains error if {
	some key in {rule_data_allowed_external_references_key, rule_data_disallowed_external_references_key}
	some index, ref in rule_data.get(key)
	pattern := object.get(ref, "url", "")
	is_string(pattern)
	pattern != ""
	rule_data.lacks_effective_anchor(pattern)
	error := {
		"message": sprintf("Pattern %q at index %d in %s is not effectively anchored with ^", [pattern, index, key]),
		"severity": "warning",
	}
}

# Warn when allowed_package_sources patterns lack effective ^ anchoring.
rule_data_errors contains error if {
	some index, source in rule_data.get(rule_data_allowed_package_sources_key)
	some pattern in source.patterns
	is_string(pattern)
	rule_data.lacks_effective_anchor(pattern)
	error := {
		"message": sprintf(
			"Pattern %q at index %d in %s is not effectively anchored with ^",
			[pattern, index, rule_data_allowed_package_sources_key],
		),
		"severity": "warning",
	}
}

# Warn when allowed_proxy_url_patterns lack effective ^ anchoring.
rule_data_errors contains error if {
	some purl_type, patterns in rule_data.get("allowed_proxy_url_patterns")
	some pattern in patterns
	is_string(pattern)
	rule_data.lacks_effective_anchor(pattern)
	error := {
		"message": sprintf(
			"Pattern %q for PURL type %q in %s is not effectively anchored with ^",
			[pattern, purl_type, "allowed_proxy_url_patterns"],
		),
		"severity": "warning",
	}
}

# Warn when disallowed_attributes except_when patterns lack effective ^ anchoring.
rule_data_errors contains error if {
	some attr_index, attr in rule_data.get(rule_data_attributes_key)
	some ew in object.get(attr, "except_when", [])
	some pattern in ew.patterns
	is_string(pattern)
	rule_data.lacks_effective_anchor(pattern)
	error := {
		"message": sprintf(
			"Pattern %q at index %d in %s except_when is not effectively anchored with ^",
			[pattern, attr_index, rule_data_attributes_key],
		),
		"severity": "warning",
	}
}

# Validate the "sbom" signing identity rule data (map shape, entry shape, and
# verification config) using the shared sigstore helper. The identity is
# optional: absent identities are silently fail-closed (there is no legacy
# config to migrate), so no "missing key" warning is emitted.
rule_data_errors contains error if {
	some error in sigstore.optional_identity_rule_data_errors(_sbom_identity_name)
}

# _sbom_identity_name is the key of the SBOM signing identity within the
# signing_identities rule data.
_sbom_identity_name := "sbom"

# _sbom_signing_identity is the named entry in the signing_identities rule
# data used to verify SBOM attestations, OCI referrers, and image-tag refs.
# Without a configured identity it defaults to an empty object; the configured
# guard prevents verification and no SBOMs from those sources are accepted.
default _sbom_signing_identity := {}

_sbom_signing_identity := sigstore.named_identity(_sbom_identity_name)

_sbom_signing_identity_configured if {
	is_object(_sbom_signing_identity)
	count(_sbom_signing_identity) > 0
}

# Collect SBOM signature verification errors for observability. Fail-closed
# exclusion happens in the verified_* wrappers; this surfaces WHY an SBOM
# was excluded, using the failures API to avoid duplicate builtin calls.
signature_verification_errors contains error if {
	_sbom_signing_identity_configured
	verification := ec.sigstore.verify_attestation(input.image.ref, _sbom_signing_identity)
	object.get(verification, "success", false) == false
	some raw_error in verification.errors
	error := sprintf("SBOM attestation signature verification failed for %s: %s", [input.image.ref, raw_error])
}

signature_verification_errors contains error if {
	_sbom_signing_identity_configured
	some result in oci.image_referrer_failures(input.image.ref, _sbom_signing_identity)
	result.item.artifactType in _sbom_artifact_types
	some raw_error in result.errors
	error := sprintf("SBOM referrer signature verification failed for %s: %s", [result.item.ref, raw_error])
}

signature_verification_errors contains error if {
	_sbom_signing_identity_configured
	some result in oci.image_tag_ref_failures(input.image.ref, _sbom_signing_identity)
	endswith(result.item, ".sbom")
	some raw_error in result.errors
	error := sprintf("SBOM tag ref signature verification failed for %s: %s", [result.item, raw_error])
}

# disallowed_attribute_excepted checks if the package's PURL has a qualifier
# matching an except_when condition, meaning the violation should be suppressed.
disallowed_attribute_excepted(disallowed, purl_string) if {
	purl_string != ""
	parsed := ec.purl.parse(purl_string)
	some exception in disallowed.except_when
	some qualifier in parsed.qualifiers
	qualifier.key == exception.purl_qualifier
	url_matches_any_pattern(qualifier.value, exception.patterns)
} else := false

# component_found_by_hermeto checks if a CycloneDX component was fetched by
# cachi2 or hermeto, based on the component's properties.
component_found_by_hermeto(component) if {
	some property in component.properties
	some name in _hermeto_names
	property == _hermeto_found_by_property(name)
} else := false

# package_found_by_hermeto checks if an SPDX package was fetched by
# cachi2 or hermeto, based on the package's annotations.
package_found_by_hermeto(pkg) if {
	some annotation in pkg.annotations
	some name in _hermeto_names
	regex.match(sprintf(`.*%s.*`, [name]), annotation.annotator)
	annotation.annotationType == "OTHER"
} else := false

# is_registry_dependency checks that an SBOM artifact (CycloneDX component
# or SPDX package) is a registry dependency: fetched from a package
# registry, not from a direct URL or VCS, and not bundled inside another
# dependency's tarball.
is_registry_dependency(parsed_purl, dependency) if {
	_is_registry_purl(parsed_purl)
	not _is_bundled(dependency)
	not _is_local_gomod_dep(parsed_purl)
}

_is_registry_purl(parsed_purl) if {
	qualifiers := {q.key | some q in object.get(parsed_purl, "qualifiers", [])}
	not "download_url" in qualifiers
	not "vcs_url" in qualifiers
}

# CycloneDX: bundled property in component.properties
_is_bundled(dependency) if {
	some property in object.get(dependency, "properties", [])
	property == _npm_bundled_property
}

# SPDX: bundled property JSON-encoded in package annotations
_is_bundled(dependency) if {
	some annotation in object.get(dependency, "annotations", [])
	annotation.annotationType == "OTHER"
	parsed := json.unmarshal(annotation.comment)
	parsed == _npm_bundled_property
}

# Hermeto counts standard libraries mentioned in go.mod as dependencies and
# includes them into SBOM. Since they won't originate from an artifact
# registry proxy they must be excluded. The same logic applies to packages
# originating from a VCS.
_is_local_gomod_dep(parsed_purl) if {
	parsed_purl.type == "golang"
	_gomod_locality_rule(parsed_purl)
}

# A gomod package will have vcs_url qualifier either if it is the main package
# or a module was obtained from local file system via a local replace.
# The relevant lines in Hermeto:
#  https://github.com/hermetoproject/hermeto/blob/db5e5f9d4f3dd2e44316a1cb2c44368c92007d54/
#	hermeto/core/package_managers/gomod/main.py#L216
#  https://github.com/hermetoproject/hermeto/blob/db5e5f9d4f3dd2e44316a1cb2c44368c92007d54/
#	hermeto/core/package_managers/gomod/main.py#L232
# go mod local replace:
#  https://go.dev/doc/modules/gomod-ref#replace
_gomod_locality_rule(parsed_purl) if {
	qualifiers := object.get(parsed_purl, "qualifiers", [])

	some qualifier in qualifiers
	qualifier.key == "vcs_url"

	# This is not supposed to happen ever, but in case it happens empty vcs_urls are forbidden.
	not _is_empty(qualifier.value)
}

# Missing version indicates that this is either the main module (i.e. the package that is
# being built which origin we cannot establish) or a standard package shipped with golang.
# In this case the version will be missing and will not be reported in SBOM:
#  https://github.com/hermetoproject/hermeto/blob/db5e5f9d4f3dd2e44316a1cb2c44368c92007d54/
#	hermeto/core/package_managers/gomod/main.py#L244
# In all other cases the version must be set to something:
#  https://go.dev/ref/mod#versions
_gomod_locality_rule(parsed_purl) if _is_empty(parsed_purl.version)

_is_empty(field) if field in {null, ""}

_npm_bundled_property := {"name": "cdx:npm:package:bundled", "value": "true"}

# hermeto_found_by_property generates the CycloneDX property object used
# to identify components fetched by the given tool name.
hermeto_found_by_property(name) := _hermeto_found_by_property(name)

_hermeto_found_by_property(name) := {
	"name": sprintf("%s:found_by", [name]),
	"value": name,
}

# Hermeto used to be known as cachi2, so we recognize the
# old name for backwards compatibility
_hermeto_names := ["cachi2", "hermeto"]

rule_data_packages_key := "disallowed_packages"

rule_data_attributes_key := "disallowed_attributes"

rule_data_allowed_external_references_key := "allowed_external_references"

rule_data_disallowed_external_references_key := "disallowed_external_references"

rule_data_allowed_package_sources_key := "allowed_package_sources"
