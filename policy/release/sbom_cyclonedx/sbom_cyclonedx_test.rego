package sbom_cyclonedx_test

import rego.v1

import data.lib.assertions
import data.lib.sbom
import data.sbom_cyclonedx

test_all_good_from_attestation if {
	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [_sbom_1_5_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_5_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
}

test_all_good_from_image if {
	files := {"root/buildinfo/content_manifests/sbom-cyclonedx.json": _sbom_1_5_attestation.statement.predicate}
	assertions.assert_empty(sbom_cyclonedx.deny) with input.image.files as files
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
}

test_not_valid if {
	expected := {{
		"code": "sbom_cyclonedx.valid_cdx_1_5",
		"msg": "CycloneDX SBOM at index 0 is not valid: components: Invalid type. Expected: array, given: string",
	}}
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components",
		"value": "spam",
	}])
	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
}

test_unsupported_version if {
	expected := {{
		"code": "sbom_cyclonedx.cdx_supported_version",
		"msg": "CycloneDX SBOM at index 0 has unsupported or missing version: 1.3",
	}}
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "replace",
		"path": "/statement/predicate/specVersion",
		"value": "1.3",
	}])
	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
}

test_valid_cdx_1_4 if {
	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [_sbom_1_4_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_4_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
}

test_invalid_cdx_1_4 if {
	expected := {{
		"code": "sbom_cyclonedx.valid_cdx_1_4",
		"msg": "CycloneDX SBOM at index 0 is not valid: components: Invalid type. Expected: array, given: string",
	}}
	att := json.patch(_sbom_1_4_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components",
		"value": "spam",
	}])
	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
}

test_valid_cdx_1_5 if {
	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [_sbom_1_5_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_5_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
}

test_invalid_cdx_1_5 if {
	expected := {{
		"code": "sbom_cyclonedx.valid_cdx_1_5",
		"msg": "CycloneDX SBOM at index 0 is not valid: components: Invalid type. Expected: array, given: string",
	}}
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components",
		"value": "spam",
	}])
	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
}

test_valid_cdx_1_6 if {
	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [_sbom_1_6_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_6_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
}

test_invalid_cdx_1_6 if {
	expected := {{
		"code": "sbom_cyclonedx.valid_cdx_1_6",
		"msg": "CycloneDX SBOM at index 0 is not valid: components: Invalid type. Expected: array, given: string",
	}}
	att := json.patch(_sbom_1_6_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components",
		"value": "spam",
	}])
	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
}

test_attributes_not_allowed_all_good if {
	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [_sbom_1_5_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_5_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []

	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [_sbom_1_5_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_5_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_attributes_key: [{"name": "attrX", "value": "valueX"}]}
}

test_attributes_not_allowed_pair if {
	expected := {{
		"code": "sbom_cyclonedx.disallowed_package_attributes",
		# regal ignore:line-length
		"term": "pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3",
		# regal ignore:line-length
		"msg": `Package pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3 has the attribute "attr1" set`,
	}}

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [_sbom_1_5_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_5_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_attributes_key: [{"name": "attr1"}]}
}

test_attributes_not_allowed_value if {
	expected := {{
		"code": "sbom_cyclonedx.disallowed_package_attributes",
		# regal ignore:line-length
		"term": "pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3",
		# regal ignore:line-length
		"msg": `Package pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3 has the attribute "attr2" set to "value2"`,
	}}

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [_sbom_1_5_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_5_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_attributes_key: [{"name": "attr2", "value": "value2"}]}
}

test_attributes_not_allowed_effective_on if {
	expected := {
		{
			"code": "sbom_cyclonedx.disallowed_package_attributes",
			# regal ignore:line-length
			"term": "pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3",
			# regal ignore:line-length
			"msg": `Package pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3 has the attribute "attr1" set`,
			"effective_on": "2025-01-01T00:00:00Z",
		},
		{
			"code": "sbom_cyclonedx.disallowed_package_attributes",
			# regal ignore:line-length
			"term": "pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3",
			# regal ignore:line-length
			"msg": `Package pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3 has the attribute "attr2" set to "value2"`,
			"effective_on": "2024-07-31T00:00:00Z",
		},
	}

	raw_results := sbom_cyclonedx.deny with input.attestations as [_sbom_1_5_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_5_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_attributes_key: [
			{"name": "attr1", "effective_on": "2025-01-01T00:00:00Z"},
			{"name": "attr2", "value": "value2"},
		]}

	results := {result_no_collections |
		some result in raw_results
		result_no_collections := json.remove(result, ["collections"])
	}

	assertions.assert_equal(expected, results)
}

test_attributes_not_allowed_value_no_purl if {
	expected := {{
		"code": "sbom_cyclonedx.disallowed_package_attributes",
		"term": "rhel",
		# regal ignore:line-length
		"msg": `Package rhel has the attribute "syft:distro:id" set to "rhel"`,
	}}

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [_sbom_1_5_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_5_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_attributes_key: [{"name": "syft:distro:id", "value": "rhel"}]}
}

test_attributes_except_when_match_suppresses_violation if {
	disallowed_attributes := [{
		"name": "hermeto:pip:package:binary",
		"value": "true",
		"except_when": [{"purl_qualifier": "repository_url", "patterns": ["^https://console\\.redhat\\.com/api/pypi/.*"]}],
	}]

	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_excepted_component(
			"pkg:pypi/some-lib@1.0?repository_url=https://console.redhat.com/api/pypi/rhoai/3.5/simple/",
			"hermeto:pip:package:binary",
			"true",
		),
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_attributes_key: disallowed_attributes}

	count({r | some r in results; r.code == "sbom_cyclonedx.disallowed_package_attributes"}) == 0
}

test_attributes_except_when_no_match_produces_violation if {
	disallowed_attributes := [{
		"name": "hermeto:pip:package:binary",
		"value": "true",
		"except_when": [{"purl_qualifier": "repository_url", "patterns": ["^https://console\\.redhat\\.com/api/pypi/.*"]}],
	}]

	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_excepted_component(
			"pkg:pypi/some-lib@1.0?repository_url=https://pypi.org/simple/",
			"hermeto:pip:package:binary",
			"true",
		),
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_attributes_key: disallowed_attributes}

	count({r | some r in results; r.code == "sbom_cyclonedx.disallowed_package_attributes"}) == 1
}

test_attributes_except_when_missing_qualifier_produces_violation if {
	disallowed_attributes := [{
		"name": "hermeto:pip:package:binary",
		"value": "true",
		"except_when": [{"purl_qualifier": "repository_url", "patterns": ["^https://console\\.redhat\\.com/api/pypi/.*"]}],
	}]

	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_excepted_component(
			"pkg:pypi/some-lib@1.0",
			"hermeto:pip:package:binary",
			"true",
		),
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_attributes_key: disallowed_attributes}

	count({r | some r in results; r.code == "sbom_cyclonedx.disallowed_package_attributes"}) == 1
}

test_attributes_except_when_no_purl_produces_violation if {
	disallowed_attributes := [{
		"name": "hermeto:pip:package:binary",
		"value": "true",
		"except_when": [{"purl_qualifier": "repository_url", "patterns": ["^https://console\\.redhat\\.com/.*"]}],
	}]

	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": {
			"type": "library",
			"name": "no-purl-component",
			"properties": [{"name": "hermeto:pip:package:binary", "value": "true"}],
		},
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_attributes_key: disallowed_attributes}

	count({r | some r in results; r.code == "sbom_cyclonedx.disallowed_package_attributes"}) == 1
}

test_attributes_except_when_multiple_patterns if {
	disallowed_attributes := [{
		"name": "hermeto:pip:package:binary",
		"value": "true",
		"except_when": [{"purl_qualifier": "repository_url", "patterns": [
			"^https://console\\.redhat\\.com/api/pypi/.*",
			"^https://packages\\.redhat\\.com/pypi/.*",
		]}],
	}]

	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_excepted_component(
			"pkg:pypi/some-lib@1.0?repository_url=https://packages.redhat.com/pypi/rhoai/",
			"hermeto:pip:package:binary",
			"true",
		),
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_attributes_key: disallowed_attributes}

	count({r | some r in results; r.code == "sbom_cyclonedx.disallowed_package_attributes"}) == 0
}

test_attributes_except_when_without_except_when_unchanged if {
	disallowed_attributes := [
		{
			"name": "hermeto:pip:package:binary",
			"value": "true",
			"except_when": [{"purl_qualifier": "repository_url", "patterns": ["^https://console\\.redhat\\.com/.*"]}],
		},
		{"name": "hermeto:bundler:package:binary", "value": "true"},
	]

	att := json.patch(_sbom_1_5_attestation, [
		{
			"op": "add",
			"path": "/statement/predicate/components/-",
			"value": _cdx_excepted_component(
				"pkg:pypi/excepted-lib@1.0?repository_url=https://console.redhat.com/api/pypi/rhoai/simple/",
				"hermeto:pip:package:binary",
				"true",
			),
		},
		{
			"op": "add",
			"path": "/statement/predicate/components/-",
			"value": _cdx_excepted_component(
				"pkg:gem/bundler-lib@1.0",
				"hermeto:bundler:package:binary",
				"true",
			),
		},
	])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_attributes_key: disallowed_attributes}

	attr_results := {r | some r in results; r.code == "sbom_cyclonedx.disallowed_package_attributes"}
	count(attr_results) == 1
	some r in attr_results
	contains(r.msg, "hermeto:bundler:package:binary")
}

test_external_references_allowed_regex_with_no_rules_is_allowed if {
	expected := {}
	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [_sbom_1_5_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_5_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_allowed_external_references_key: []}
}

test_external_references_allowed_regex if {
	expected := {{
		"code": "sbom_cyclonedx.allowed_package_external_references",
		# regal ignore:line-length
		"term": "pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3",
		# regal ignore:line-length
		"msg": `Package pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3 has reference "https://example.com/file.txt" of type "distribution" which is not explicitly allowed by pattern ".*allowed.net.*"`,
	}}

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [_sbom_1_5_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_5_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_allowed_external_references_key: [{
			"type": "distribution",
			"url": ".*allowed.net.*",
		}]}
}

test_external_references_allowed_no_purl if {
	expected := {{
		"code": "sbom_cyclonedx.allowed_package_external_references",
		"term": "rhel",
		# regal ignore:line-length
		"msg": `Package rhel has reference "https://www.redhat.com/" of type "website" which is not explicitly allowed by pattern ".*example.com.*"`,
	}}

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [_sbom_1_5_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_5_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_allowed_external_references_key: [{
			"type": "website",
			"url": ".*example.com.*",
		}]}
}

test_external_references_disallowed_regex if {
	expected := {{
		"code": "sbom_cyclonedx.disallowed_package_external_references",
		# regal ignore:line-length
		"term": "pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3",
		# regal ignore:line-length
		"msg": `Package pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3 has reference "https://example.com/file.txt" of type "distribution" which is disallowed by pattern ".*example.com.*"`,
	}}

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [_sbom_1_5_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_5_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_disallowed_external_references_key: [{
			"type": "distribution",
			"url": ".*example.com.*",
		}]}
}

test_external_references_disallowed_no_purl if {
	expected := {{
		"code": "sbom_cyclonedx.disallowed_package_external_references",
		"term": "rhel",
		# regal ignore:line-length
		"msg": `Package rhel has reference "https://www.redhat.com/" of type "website" which is disallowed by pattern ".*redhat.com.*"`,
	}}

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [_sbom_1_5_attestation]
		with sbom._verified_sbom_attestations as [_sbom_1_5_attestation]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_disallowed_external_references_key: [{
			"type": "website",
			"url": ".*redhat.com.*",
		}]}
}

test_allowed_package_sources if {
	expected := {{
		"code": "sbom_cyclonedx.allowed_package_sources",
		"term": "pkg:generic/openssl@1.1.10g?download_url=https://openssl.org/source/openssl-1.1.0g.tar.gz",
		# regal ignore:line-length
		"msg": `Package pkg:generic/openssl@1.1.10g?download_url=https://openssl.org/source/openssl-1.1.0g.tar.gz fetched by Hermeto was sourced from "https://openssl.org/source/openssl-1.1.0g.tar.gz" which is not allowed`,
	}}

	att := json.patch(_sbom_1_5_attestation, [
		{
			"op": "add",
			"path": "/statement/predicate/components/-",
			"value": {
				"type": "file",
				"name": "openssl",
				"purl": "pkg:generic/openssl@1.1.10g?download_url=https://openssl.org/source/openssl-1.1.0g.tar.gz",
				"properties": [{
					"name": "hermeto:found_by",
					"value": "hermeto",
				}],
				"externalReferences": [{"type": "distribution", "url": "https://openssl.org/source/openssl-1.1.0g.tar.gz"}],
			},
		},
		{
			"op": "add",
			"path": "/statement/predicate/components/-",
			"value": {
				"type": "library",
				"name": "batik-anim",
				"purl": "pkg:maven/org.apache.xmlgraphics/batik-anim@1.9.1?type=pom",
				"properties": [{
					"name": "hermeto:found_by",
					"value": "hermeto",
				}],
				# regal ignore:line-length
				"externalReferences": [{"type": "distribution", "url": "https://repo.maven.apache.org/maven2/org/apache/xmlgraphics/batik-anim/1.9.1/batik-anim-1.9.1.pom"}],
			},
		},
		{
			"op": "add",
			"path": "/statement/predicate/components/-",
			"value": {
				"type": "file",
				"name": "unrelated",
				"purl": "pkg:generic/unrelated",
				"externalReferences": [{"type": "distribution", "url": "https://irrelevant.org"}],
			},
		},
	])

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_allowed_package_sources_key: [
			{
				"type": "maven",
				"patterns": [".*apache.org.*", ".*example.com.*"],
			},
			{
				"type": "generic",
				"patterns": [".*apache.org.*", ".*example.com.*"],
			},
		]}
}

test_allowed_package_sources_no_rule_defined if {
	expected := {{
		"code": "sbom_cyclonedx.allowed_package_sources",
		"term": "pkg:maven/org.apache.xmlgraphics/batik-anim@1.9.1?type=pom",
		# regal ignore:line-length
		"msg": `Package pkg:maven/org.apache.xmlgraphics/batik-anim@1.9.1?type=pom fetched by Hermeto was sourced from "https://repo.maven.apache.org/maven2/org/apache/xmlgraphics/batik-anim/1.9.1/batik-anim-1.9.1.pom" which is not allowed`,
	}}

	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": {
			"type": "library",
			"name": "batik-anim",
			"purl": "pkg:maven/org.apache.xmlgraphics/batik-anim@1.9.1?type=pom",
			"properties": [{
				"name": "hermeto:found_by",
				"value": "hermeto",
			}],
			# regal ignore:line-length
			"externalReferences": [{"type": "distribution", "url": "https://repo.maven.apache.org/maven2/org/apache/xmlgraphics/batik-anim/1.9.1/batik-anim-1.9.1.pom"}],
		},
	}])

	# rule data is defined only for purl of type generic
	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_allowed_package_sources_key: [{
			"type": "generic",
			"patterns": [".*example.com.*"],
		}]}
}

test_attributes_not_allowed_no_properties if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "remove",
		"path": "/statement/predicate/components/0/properties",
	}])

	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {sbom.rule_data_attributes_key: [{"name": "attr", "value": "value"}]}
}

test_allowed_by_default if {
	assert_allowed("pkg:golang/k8s.io/client-go@v0.28.3", [])
}

test_not_allowed_with_min if {
	disallowed_packages := [{
		"purl": "pkg:golang/k8s.io/client-go",
		"format": "semverv",
		"min": "v50.28.3",
	}]

	# Much lower than min version
	assert_allowed("pkg:golang/k8s.io/client-go@v0.29.4", disallowed_packages)

	# Lower than min version
	assert_allowed("pkg:golang/k8s.io/client-go@v50.28.2", disallowed_packages)

	# Exact match to min version
	assert_not_allowed("pkg:golang/k8s.io/client-go@v50.28.3", disallowed_packages)

	# Higher than min version
	assert_not_allowed("pkg:golang/k8s.io/client-go@v50.28.4", disallowed_packages)

	# Much higher than min version
	assert_not_allowed("pkg:golang/k8s.io/client-go@v99.99.99", disallowed_packages)
}

test_not_allowed_with_max if {
	disallowed_packages := [{
		"purl": "pkg:golang/k8s.io/client-go",
		"format": "semverv",
		"max": "v50.28.3",
	}]

	# Much lower than max version
	assert_not_allowed("pkg:golang/k8s.io/client-go@v0.29.4", disallowed_packages)

	# Lower than max version
	assert_not_allowed("pkg:golang/k8s.io/client-go@v50.28.2", disallowed_packages)

	# Exact match to max version
	assert_not_allowed("pkg:golang/k8s.io/client-go@v50.28.3", disallowed_packages)

	# Higher than max version
	assert_allowed("pkg:golang/k8s.io/client-go@v50.28.4", disallowed_packages)

	# Much higher than max version
	assert_allowed("pkg:golang/k8s.io/client-go@v99.99.99", disallowed_packages)
}

test_not_allowed_with_subpaths if {
	disallowed_packages := [{
		"purl": "pkg:golang/github.com/hashicorp/consul",
		"format": "semverv",
		"min": "v1.29.2",
		"exceptions": [
			{"subpath": "api"},
			{"subpath": "sdk"},
		],
	}]

	# Unknown subpath matches
	assert_not_allowed("pkg:golang/github.com/hashicorp/consul@v1.29.2#spam", disallowed_packages)

	# Missing subpath matches
	assert_not_allowed("pkg:golang/github.com/hashicorp/consul@v1.29.2#", disallowed_packages)
	assert_not_allowed("pkg:golang/github.com/hashicorp/consul@v1.29.2", disallowed_packages)

	# Excluded subpaths do not match
	assert_allowed("pkg:golang/github.com/hashicorp/consul@v1.29.2#api", disallowed_packages)
	assert_allowed("pkg:golang/github.com/hashicorp/consul@v1.29.2#sdk", disallowed_packages)
}

test_not_allowed_with_min_max if {
	disallowed_packages := [{
		"purl": "pkg:golang/k8s.io/client-go",
		"format": "semverv",
		"min": "v50.20.2",
		"max": "v50.28.3",
	}]

	# Much lower than min version
	assert_allowed("pkg:golang/k8s.io/client-go@v0.29.4", disallowed_packages)

	# Lower than min version
	assert_allowed("pkg:golang/k8s.io/client-go@v50.20.1", disallowed_packages)

	# Exact match to min version
	assert_not_allowed("pkg:golang/k8s.io/client-go@v50.20.2", disallowed_packages)

	# Mid-range
	assert_not_allowed("pkg:golang/k8s.io/client-go@v50.24.9", disallowed_packages)

	# Exact match to max version
	assert_not_allowed("pkg:golang/k8s.io/client-go@v50.28.3", disallowed_packages)

	# Higher than max version
	assert_allowed("pkg:golang/k8s.io/client-go@v50.28.4", disallowed_packages)

	# Much higher than max version
	assert_allowed("pkg:golang/k8s.io/client-go@v99.99.99", disallowed_packages)
}

assert_allowed(purl, disallowed_packages) if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/0/purl",
		"value": purl,
	}])

	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [att] # regal ignore:with-outside-test-context
		with sbom._verified_sbom_attestations as [att]
		with ec.oci.image_referrers as [] # regal ignore:with-outside-test-context
		with ec.oci.image_tag_refs as [] # regal ignore:with-outside-test-context
		with data.rule_data.disallowed_packages as disallowed_packages # regal ignore:with-outside-test-context
		with data.rule_data.vendored_purl_types as [] # regal ignore:with-outside-test-context
}

assert_not_allowed(purl, disallowed_packages) if {
	expected := {{
		"code": "sbom_cyclonedx.allowed",
		"msg": sprintf("Package is not allowed: %s", [purl]),
	}}
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/0/purl",
		"value": purl,
	}])

	# regal ignore:with-outside-test-context
	assertions.assert_equal_results(sbom_cyclonedx.deny, expected) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att] # regal ignore:with-outside-test-context
		with ec.oci.image_referrers as [] # regal ignore:with-outside-test-context
		with ec.oci.image_tag_refs as [] # regal ignore:with-outside-test-context
		with data.rule_data.disallowed_packages as disallowed_packages # regal ignore:with-outside-test-context
		with data.rule_data.vendored_purl_types as [] # regal ignore:with-outside-test-context
}

_sbom_1_5_attestation := {"statement": {
	"predicateType": "https://cyclonedx.org/bom",
	"predicate": {
		"$schema": "http://cyclonedx.org/schema/bom-1.5.schema.json",
		"bomFormat": "CycloneDX",
		"specVersion": "1.5",
		"serialNumber": "urn:uuid:cf1a2c3d-bcf8-45c4-9d0f-b2b59a0753f0",
		"version": 1,
		"metadata": {
			"timestamp": "2023-11-20T17:32:41Z",
			"tools": [{
				"vendor": "anchore",
				"name": "syft",
				"version": "0.96.0",
			}],
			"component": {
				"bom-ref": "158c8a990fbd4038",
				"type": "file",
				"name": "/var/lib/containers/storage/vfs/dir/dfd74fe178f4ea0472b5569bff38a4df69d05e7a81b538c98d731566aec15a69",
			},
		},
		"components": [
			{
				# regal ignore:line-length
				"bom-ref": "pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3&package-id=f4f4e3cc2a6d9c37",
				"type": "library",
				"publisher": "Red Hat, Inc.",
				"name": "coreutils-single",
				"version": "8.32-34.el9",
				"licenses": [{"license": {"name": "GPLv3+"}}],
				"cpe": "cpe:2.3:a:coreutils-single:coreutils-single:8.32-34.el9:*:*:*:*:*:*:*",
				# regal ignore:line-length
				"purl": "pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3",
				"properties": [
					{"name": "attr1"},
					{
						"name": "attr2",
						"value": "value2",
					},
				],
				"externalReferences": [{
					"type": "distribution",
					"url": "https://example.com/file.txt",
				}],
			},
			{
				"bom-ref": "os:rhel@9.4",
				"type": "operating-system",
				"name": "rhel",
				"version": "9.4",
				"description": "Red Hat Enterprise Linux 9.4 (Plow)",
				"cpe": "cpe:2.3:o:redhat:enterprise_linux:9:*:baseos:*:*:*:*:*",
				"swid": {
					"tagId": "rhel",
					"name": "rhel",
					"version": "9.4",
				},
				"externalReferences": [
					{
						"url": "https://bugzilla.redhat.com/",
						"type": "issue-tracker",
					},
					{
						"url": "https://www.redhat.com/",
						"type": "website",
					},
				],
				"properties": [
					{
						"name": "syft:distro:id",
						"value": "rhel",
					},
					{
						"name": "syft:distro:idLike:0",
						"value": "fedora",
					},
					{
						"name": "syft:distro:prettyName",
						"value": "Red Hat Enterprise Linux 9.4 (Plow)",
					},
					{
						"name": "syft:distro:versionID",
						"value": "9.4",
					},
				],
			},
		],
	},
}}

_sbom_1_4_attestation := json.patch(_sbom_1_5_attestation, [
	{
		"op": "replace",
		"path": "/statement/predicate/$schema",
		"value": "http://cyclonedx.org/schema/bom-1.4.schema.json",
	},
	{
		"op": "replace",
		"path": "/statement/predicate/specVersion",
		"value": "1.4",
	},
])

_sbom_1_6_attestation := json.patch(_sbom_1_5_attestation, [
	{
		"op": "replace",
		"path": "/statement/predicate/$schema",
		"value": "http://cyclonedx.org/schema/bom-1.6.schema.json",
	},
	{
		"op": "replace",
		"path": "/statement/predicate/specVersion",
		"value": "1.6",
	},
])

test_proxy_url_cyclonedx_allowed if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_proxy_component(
			"pkg:maven/org.example/lib@1.0",
			"https://proxy.example.com/maven/org/example/lib-1.0.jar",
		),
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data

	count({r | some r in results; r.code == "sbom_cyclonedx.allowed_proxy_urls"}) == 0
}

test_proxy_url_cyclonedx_denied if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_proxy_component(
			"pkg:maven/org.example/lib@1.0",
			"https://evil.com/lib-1.0.jar",
		),
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data

	proxy_results := {r | some r in results; r.code == "sbom_cyclonedx.allowed_proxy_urls"}
	count(proxy_results) == 1
}

test_proxy_url_cyclonedx_multiple_distribution_refs if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": {
			"type": "library",
			"name": "component",
			"purl": "pkg:maven/org.example/lib@1.0",
			"properties": [{"name": "hermeto:found_by", "value": "hermeto"}],
			"externalReferences": [
				{"type": "distribution", "comment": "proxy URL", "url": "https://proxy.example.com/maven/org/example/lib-1.0.jar"},
				{"type": "distribution", "comment": "proxy URL", "url": "https://evil.com/lib-1.0.jar"},
			],
		},
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data

	proxy_results := {r | some r in results; r.code == "sbom_cyclonedx.allowed_proxy_urls"}
	count(proxy_results) == 1
}

test_proxy_url_cyclonedx_empty_enabled_purl_types if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_proxy_component(
			"pkg:maven/org.example/lib@1.0",
			"https://evil.com/lib-1.0.jar",
		),
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {
			"proxy_enabled_purl_types": [],
			"allowed_proxy_url_patterns": {"maven": ["^https://proxy\\.example\\.com/maven/.*"]},
		}

	count({r | some r in results; r.code == "sbom_cyclonedx.allowed_proxy_urls"}) == 0
}

test_proxy_url_cyclonedx_enabled_type_no_patterns if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_proxy_component(
			"pkg:pypi/example-lib@1.0",
			"https://pypi.org/packages/example-lib-1.0.tar.gz",
		),
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {
			"proxy_enabled_purl_types": ["maven", "pypi"],
			"allowed_proxy_url_patterns": {"maven": ["^https://proxy\\.example\\.com/maven/.*"]},
		}

	proxy_results := {r | some r in results; r.code == "sbom_cyclonedx.allowed_proxy_urls"}
	count(proxy_results) == 1
}

test_proxy_url_cyclonedx_non_proxy_purl_type if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_proxy_component(
			"pkg:golang/example.com/lib@1.0",
			"https://anything.com/lib-1.0.tar.gz",
		),
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data

	count({r | some r in results; r.code == "sbom_cyclonedx.allowed_proxy_urls"}) == 0
}

test_proxy_url_cyclonedx_not_hermeto_skipped if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": {
			"type": "library",
			"name": "component",
			"purl": "pkg:maven/org.example/lib@1.0",
			"externalReferences": [{"type": "distribution", "url": "https://evil.com/lib-1.0.jar"}],
		},
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data

	count({r | some r in results; r.code == "sbom_cyclonedx.allowed_proxy_urls"}) == 0
}

test_proxy_url_cyclonedx_download_url_skipped if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_proxy_component(
			"pkg:maven/org.example/lib@1.0?download_url=https://example.com/lib.jar",
			"https://evil.com/lib-1.0.jar",
		),
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data

	count({r | some r in results; r.code == "sbom_cyclonedx.allowed_proxy_urls"}) == 0
}

test_proxy_url_cyclonedx_bundled_skipped if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_bundled_component("pkg:npm/example-lib@2.0"),
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data

	count({r | some r in results; r.code == "sbom_cyclonedx.allowed_proxy_urls"}) == 0
}

test_proxy_metadata_required_cdx_bundled_passes if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_bundled_component("pkg:npm/example-lib@2.0"),
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data

	count({r | some r in results; r.code == "sbom_cyclonedx.proxy_metadata_required"}) == 0
}

_cdx_bundled_component(purl) := {
	"type": "library",
	"name": "component",
	"purl": purl,
	"properties": [
		{"name": "hermeto:found_by", "value": "hermeto"},
		{"name": "cdx:npm:package:bundled", "value": "true"},
	],
}

_cdx_excepted_component(purl, attr_name, attr_value) := {
	"type": "library",
	"name": "excepted-component",
	"purl": purl,
	"properties": [{"name": attr_name, "value": attr_value}],
}

_cdx_proxy_component(purl, distribution_url) := {
	"type": "library",
	"name": "component",
	"purl": purl,
	"properties": [{"name": "hermeto:found_by", "value": "hermeto"}],
	"externalReferences": [{"type": "distribution", "comment": "proxy URL", "url": distribution_url}],
}

_cdx_hermeto_component(purl, external_refs) := {
	"type": "library",
	"name": "component",
	"purl": purl,
	"properties": [{"name": "hermeto:found_by", "value": "hermeto"}],
	"externalReferences": external_refs,
}

_proxy_rule_data := {
	"proxy_enabled_purl_types": ["maven", "npm"],
	"allowed_proxy_url_patterns": {
		"maven": ["^https://proxy\\.example\\.com/maven/.*"],
		"npm": ["^https://proxy\\.example\\.com/npm/.*"],
	},
}

# proxy_metadata_required tests

test_proxy_metadata_required_cdx_denied if {
	expected := {{
		"code": "sbom_cyclonedx.proxy_metadata_required",
		"term": "pkg:maven/org.example/lib@1.0",
		# regal ignore:line-length
		"msg": `Package pkg:maven/org.example/lib@1.0 is missing proxy metadata (no externalReference of type "distribution" with comment "proxy URL")`,
	}}

	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_hermeto_component("pkg:maven/org.example/lib@1.0", []),
	}])

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data
}

test_proxy_metadata_required_cdx_with_distribution_passes if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_hermeto_component(
			"pkg:maven/org.example/lib@1.0",
			[{"type": "distribution", "comment": "proxy URL", "url": "https://proxy.example.com/maven/lib-1.0.jar"}],
		),
	}])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data

	count({r | some r in results; r.code == "sbom_cyclonedx.proxy_metadata_required"}) == 0
}

test_proxy_metadata_required_cdx_not_hermeto_passes if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": {
			"type": "library",
			"name": "component",
			"purl": "pkg:maven/org.example/lib@1.0",
			"properties": [{"name": "other:property", "value": "other"}],
		},
	}])

	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data
}

test_proxy_metadata_required_cdx_non_distribution_refs_denied if {
	expected := {{
		"code": "sbom_cyclonedx.proxy_metadata_required",
		"term": "pkg:maven/org.example/lib@1.0",
		# regal ignore:line-length
		"msg": `Package pkg:maven/org.example/lib@1.0 is missing proxy metadata (no externalReference of type "distribution" with comment "proxy URL")`,
	}}

	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_hermeto_component(
			"pkg:maven/org.example/lib@1.0",
			[{"type": "vcs", "url": "https://github.com/example/lib.git"}],
		),
	}])

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data
}

test_proxy_metadata_required_cdx_non_proxy_purl_type_passes if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_hermeto_component("pkg:golang/example.com/lib@1.0", []),
	}])

	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data
}

test_proxy_metadata_required_cdx_download_url_passes if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_hermeto_component(
			"pkg:maven/org.example/lib@1.0?download_url=https://example.com/lib.jar",
			[],
		),
	}])

	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data
}

test_proxy_metadata_required_cdx_vcs_url_passes if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_hermeto_component(
			"pkg:maven/org.example/lib@1.0?vcs_url=https://github.com/example/lib.git",
			[],
		),
	}])

	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _proxy_rule_data
}

# experimental_hermeto_backend tests

test_experimental_hermeto_backend_cdx_denied if {
	expected := {{
		"code": "sbom_cyclonedx.experimental_hermeto_backend",
		"term": "pkg:golang/example.com/foo@1.0.0",
		# regal ignore:line-length
		"msg": `Package pkg:golang/example.com/foo@1.0.0 was fetched using experimental Hermeto backend "hermeto:backend:experimental:x-pnpm"`,
	}}

	att := json.patch(_sbom_1_5_attestation, [
		{
			"op": "add",
			"path": "/statement/predicate/components/-",
			"value": _cdx_backend_component("pkg:golang/example.com/foo@1.0.0"),
		},
		{
			"op": "add",
			"path": "/statement/predicate/annotations",
			"value": [_cdx_backend_annotation("pkg:golang/example.com/foo@1.0.0", "hermeto:backend:experimental:x-pnpm")],
		},
	])

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data.vendored_purl_types as []
}

test_experimental_hermeto_backend_cdx_stable_passes if {
	att := json.patch(_sbom_1_5_attestation, [
		{
			"op": "add",
			"path": "/statement/predicate/components/-",
			"value": _cdx_backend_component("pkg:golang/example.com/foo@1.0.0"),
		},
		{
			"op": "add",
			"path": "/statement/predicate/annotations",
			"value": [_cdx_backend_annotation("pkg:golang/example.com/foo@1.0.0", "hermeto:backend:gomod")],
		},
	])

	results := sbom_cyclonedx.deny with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []

	count({r | some r in results; r.code == "sbom_cyclonedx.experimental_hermeto_backend"}) == 0
}

test_experimental_hermeto_backend_cdx_no_purl_denied if {
	expected := {{
		"code": "sbom_cyclonedx.experimental_hermeto_backend",
		"term": "component-no-purl",
		# regal ignore:line-length
		"msg": `Package component-no-purl was fetched using experimental Hermeto backend "hermeto:backend:experimental:x-pnpm"`,
	}}

	att := json.patch(_sbom_1_5_attestation, [
		{
			"op": "add",
			"path": "/statement/predicate/components/-",
			"value": _cdx_backend_component_no_purl,
		},
		{
			"op": "add",
			"path": "/statement/predicate/annotations",
			"value": [_cdx_backend_annotation("component-no-purl-ref", "hermeto:backend:experimental:x-pnpm")],
		},
	])

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
}

test_experimental_hermeto_backend_cdx_mixed_annotations if {
	expected := {{
		"code": "sbom_cyclonedx.experimental_hermeto_backend",
		"term": "pkg:golang/example.com/foo@1.0.0",
		# regal ignore:line-length
		"msg": `Package pkg:golang/example.com/foo@1.0.0 was fetched using experimental Hermeto backend "hermeto:backend:experimental:x-pnpm"`,
	}}

	att := json.patch(_sbom_1_5_attestation, [
		{
			"op": "add",
			"path": "/statement/predicate/components/-",
			"value": _cdx_backend_component("pkg:golang/example.com/foo@1.0.0"),
		},
		{
			"op": "add",
			"path": "/statement/predicate/annotations",
			"value": [
				_cdx_backend_annotation("pkg:golang/example.com/foo@1.0.0", "hermeto:backend:gomod"),
				_cdx_backend_annotation("pkg:golang/example.com/foo@1.0.0", "hermeto:backend:experimental:x-pnpm"),
			],
		},
	])

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data.vendored_purl_types as []
}

# hermeto_attribution_required tests

test_hermeto_attribution_required_cdx_with_hermeto_passes if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_vendored_component("pkg:golang/example.com/lib@1.0", true),
	}])

	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _vendored_rule_data
}

test_hermeto_attribution_required_cdx_without_hermeto_denied if {
	expected := {{
		"code": "sbom_cyclonedx.hermeto_attribution_required",
		"term": "pkg:golang/example.com/lib@1.0",
		# regal ignore:line-length
		"msg": `Package pkg:golang/example.com/lib@1.0 has PURL type "golang" which requires Hermeto attribution but was not processed by Hermeto`,
	}}

	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_vendored_component("pkg:golang/example.com/lib@1.0", false),
	}])

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _vendored_rule_data
}

test_hermeto_attribution_required_cdx_mixed if {
	expected := {{
		"code": "sbom_cyclonedx.hermeto_attribution_required",
		"term": "pkg:golang/example.com/bad@2.0",
		# regal ignore:line-length
		"msg": `Package pkg:golang/example.com/bad@2.0 has PURL type "golang" which requires Hermeto attribution but was not processed by Hermeto`,
	}}

	att := json.patch(_sbom_1_5_attestation, [
		{
			"op": "add",
			"path": "/statement/predicate/components/-",
			"value": _cdx_vendored_component("pkg:golang/example.com/good@1.0", true),
		},
		{
			"op": "add",
			"path": "/statement/predicate/components/-",
			"value": _cdx_vendored_component("pkg:golang/example.com/bad@2.0", false),
		},
	])

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _vendored_rule_data
}

test_hermeto_attribution_required_cdx_unconfigured_type_passes if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_vendored_component("pkg:npm/example-lib@2.0", false),
	}])

	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _vendored_rule_data
}

test_hermeto_attribution_required_cdx_local_dep_passes if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_vendored_component("pkg:golang/example.com/lib@1.0?vcs_url=https://github.com/example/lib.git", false),
	}])

	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _vendored_rule_data
}

test_hermeto_attribution_required_cdx_empty_rule_data_passes if {
	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_vendored_component("pkg:golang/example.com/lib@1.0", false),
	}])

	assertions.assert_empty(sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as {"vendored_purl_types": []}
}

test_hermeto_attribution_required_cdx_cargo_denied if {
	expected := {{
		"code": "sbom_cyclonedx.hermeto_attribution_required",
		"term": "pkg:cargo/serde@1.0.0",
		# regal ignore:line-length
		"msg": `Package pkg:cargo/serde@1.0.0 has PURL type "cargo" which requires Hermeto attribution but was not processed by Hermeto`,
	}}

	att := json.patch(_sbom_1_5_attestation, [{
		"op": "add",
		"path": "/statement/predicate/components/-",
		"value": _cdx_vendored_component("pkg:cargo/serde@1.0.0", false),
	}])

	assertions.assert_equal_results(expected, sbom_cyclonedx.deny) with input.attestations as [att]
		with sbom._verified_sbom_attestations as [att]
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with data.rule_data as _vendored_rule_data
}

_cdx_vendored_component(purl, with_hermeto) := component if {
	with_hermeto == true
	component := {
		"type": "library",
		"name": "component",
		"purl": purl,
		"properties": [{"name": "hermeto:found_by", "value": "hermeto"}],
	}
} else := component if {
	component := {
		"type": "library",
		"name": "component",
		"purl": purl,
		"properties": [],
	}
}

_vendored_rule_data := {"vendored_purl_types": ["golang", "cargo"]}

_cdx_backend_component(purl) := {
	"bom-ref": purl,
	"type": "library",
	"name": "component",
	"purl": purl,
}

_cdx_backend_component_no_purl := {
	"bom-ref": "component-no-purl-ref",
	"type": "library",
	"name": "component-no-purl",
}

_cdx_backend_annotation(subject, text) := {
	"subjects": [subject],
	"annotator": {"organization": {"name": "red hat"}},
	"timestamp": "2026-05-01T12:00:00Z",
	"text": text,
}
