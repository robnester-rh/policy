package sbom_test

import rego.v1

import data.lib
import data.lib.assertions
import data.sbom

test_not_found if {
	expected := {{"code": "sbom.found", "msg": "No SBOM attestations found"}}
	assertions.assert_equal_results(expected, sbom.deny) with input.attestations as []
		with lib.sbom._verified_sbom_attestations as []
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with input.image.ref as "registry.local/spam@sha256:1230000000000000000000000000000000000000000000000000000000000123"
}

test_not_found_image_index if {
	att := {"statement": {
		"predicateType": "https://slsa.dev/provenance/v0.2",
		"predicate": {
			"buildType": lib.tekton_pipeline_run,
			"buildConfig": {"tasks": [{"results": [
				{
					"name": "IMAGES",
					"type": "string",
					"value": "registry.local/spam@sha256:abc, registry.local/bacon@sha256:bcd",
				},
				{
					"name": "IMAGE_URL",
					"type": "string",
					"value": "registry.local/eggs:latest",
				},
				{
					"name": "IMAGE_DIGEST",
					"type": "string",
					"value": "sha256:fff0000000000000000000000000000000000000000000000000000000000fff",
				},
			]}]},
		},
	}}

	assertions.assert_empty(sbom.deny) with input.attestations as [att]
		with lib.sbom._verified_sbom_attestations as [att]
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with input.image.ref as "registry.local/ham@sha256:fff0000000000000000000000000000000000000000000000000000000000fff"
}

test_rule_data_validation if {
	d := {
		"disallowed_packages": [
			# Missing required attributes
			{},
			# Additional properties not allowed
			{"purl": "pkg:golang/k8s.io/client-go", "format": "semverv", "min": "v0.1.0", "blah": "foo"},
			# Bad types everywhere
			{"purl": 1, "format": 2, "min": 3, "max": 4, "exceptions": [{"subpath": 1}]},
			# Duplicated items
			{"purl": "pkg:golang/k8s.io/client-go", "format": "semverv", "min": "v0.1.0"},
			{"purl": "pkg:golang/k8s.io/client-go", "format": "semverv", "min": "v0.1.0"},
			# Bad semver values
			{"purl": "pkg:golang/k8s.io/client-go", "format": "semverv", "min": "v0.1"},
			{"purl": "pkg:golang/k8s.io/client-go", "format": "semver", "max": "v0.1"},
		],
		lib.sbom.rule_data_attributes_key: [
			# ok
			{"name": "some_attr", "value": "some_val"},
			{"name": "no_val_attr"},
			# Missing required attributes
			{},
			# Additional properties not allowed
			{"name": "_name_", "value": "_value_", "something": "else"},
			# Bad types everywhere
			{"name": 1, "value": 2},
			# Duplicated items
			{"name": "_name_", "value": "_value_"},
			{"name": "_name_", "value": "_value_"},
			# Invalid effective on format
			{"name": "_name_", "effective_on": "not-a-date"},
			# Invalid regex in except_when
			{"name": "bad_regex_attr", "except_when": [{"purl_qualifier": "repo", "patterns": ["["]}]},
		],
		lib.sbom.rule_data_allowed_external_references_key: [
			{"type": "distribution", "url": "example.com"},
			{"invalid": "foo"},
		],
		lib.sbom.rule_data_disallowed_external_references_key: [
			{"type": "distribution", "url": "badurl"},
			{"invalid": "foo"},
		],
		lib.sbom.rule_data_allowed_package_sources_key: [
			{"type": "generic", "patterns": ["["]},
			{"invalid": "foo"},
		],
	}

	expected := {
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_packages has unexpected format: 0: Must validate at least one schema (anyOf)",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_packages has unexpected format: 0: format is required",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_packages has unexpected format: 0: min is required",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_packages has unexpected format: 0: purl is required",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_packages has unexpected format: 1: Additional property blah is not allowed",
			"severity": "warning",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Rule data disallowed_packages has unexpected format: 2.format: 2.format must be one of the following: \"semver\", \"semverv\"",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_packages has unexpected format: 2.max: Invalid type. Expected: string, given: integer",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_packages has unexpected format: 2.min: Invalid type. Expected: string, given: integer",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_packages has unexpected format: 2.purl: Invalid type. Expected: string, given: integer",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Item at index 2 in disallowed_packages does not have a valid PURL: '\\x01'",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_packages has unexpected format: (Root): array items[3,4] must be unique",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Item at index 5 in disallowed_packages does not have a valid min semver value: \"0.1\"",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Item at index 6 in disallowed_packages does not have a valid max semver value: \"0.1\"",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_attributes has unexpected format: 2: name is required",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_attributes has unexpected format: 3: Additional property something is not allowed",
			"severity": "warning",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Rule data disallowed_attributes has unexpected format: 4.name: Invalid type. Expected: string, given: integer",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Rule data disallowed_attributes has unexpected format: 4.value: Invalid type. Expected: string, given: integer",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_attributes has unexpected format: (Root): array items[5,6] must be unique",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_attributes has unexpected format: 7.effective_on: Does not match format 'date-time'",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Rule data disallowed_attributes has unexpected format: 8.except_when.0.patterns.0: Does not match format 'regex'",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Item at index 8 in disallowed_attributes has an invalid regular expression in except_when: \"[\"",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data allowed_external_references has unexpected format: 1: Additional property invalid is not allowed",
			"severity": "warning",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data allowed_external_references has unexpected format: 1: type is required",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data allowed_external_references has unexpected format: 1: url is required",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data allowed_package_sources has unexpected format: 0.patterns.0: Does not match format 'regex'",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data allowed_package_sources has unexpected format: 1: Additional property invalid is not allowed",
			"severity": "warning",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data allowed_package_sources has unexpected format: 1: patterns is required",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data allowed_package_sources has unexpected format: 1: type is required",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Rule data disallowed_external_references has unexpected format: 1: Additional property invalid is not allowed",
			"severity": "warning",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_external_references has unexpected format: 1: type is required",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "Rule data disallowed_external_references has unexpected format: 1: url is required",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Rule data disallowed_packages has unexpected format: 2.exceptions.0.subpath: Invalid type. Expected: string, given: integer",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Pattern \"example.com\" at index 0 in allowed_external_references is not effectively anchored with ^",
			"severity": "warning",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Pattern \"badurl\" at index 0 in disallowed_external_references is not effectively anchored with ^",
			"severity": "warning",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Pattern \"[\" at index 0 in allowed_package_sources is not effectively anchored with ^",
			"severity": "warning",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Pattern \"[\" at index 8 in disallowed_attributes except_when is not effectively anchored with ^",
			"severity": "warning",
		},
	}

	assertions.assert_equal_results(sbom.deny, expected) with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as d

	# rule data keys are optional
	assertions.assert_empty(sbom.deny) with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as {}
	assertions.assert_empty(sbom.deny) with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as {
			lib.sbom.rule_data_packages_key: [],
			lib.sbom.rule_data_attributes_key: [],
			lib.sbom.rule_data_allowed_package_sources_key: [],
		}

	# valid except_when entry passes schema validation
	assertions.assert_empty(sbom.deny) with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as {lib.sbom.rule_data_attributes_key: [{
			"name": "hermeto:pip:package:binary",
			"value": "true",
			"except_when": [{"purl_qualifier": "repository_url", "patterns": ["^https://console\\.redhat\\.com/.*"]}],
		}]}
}

test_proxy_rule_data_validation if {
	# Valid data - no errors
	assertions.assert_empty(sbom.deny) with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as {
			"proxy_enabled_purl_types": ["maven", "npm"],
			"allowed_proxy_url_patterns": {"maven": ["^https://proxy\\.example\\.com/.*"]},
		}

	# Invalid proxy_enabled_purl_types: wrong type
	d_bad_purl_types := {
		"proxy_enabled_purl_types": [1, "maven", "maven"],
		"allowed_proxy_url_patterns": {},
	}
	expected_bad_purl_types := {
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Rule data proxy_enabled_purl_types has unexpected format: 0: Invalid type. Expected: string, given: integer",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Rule data proxy_enabled_purl_types has unexpected format: (Root): array items[1,2] must be unique",
			"severity": "failure",
		},
	}
	assertions.assert_equal_results(expected_bad_purl_types, sbom.deny) with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as d_bad_purl_types

	# Invalid allowed_proxy_url_patterns: wrong value type
	d_bad_patterns := {
		"proxy_enabled_purl_types": [],
		"allowed_proxy_url_patterns": {"maven": "not-an-array"},
	}
	expected_bad_patterns := {{
		"code": "sbom.disallowed_packages_provided",
		# regal ignore:line-length
		"msg": "Rule data allowed_proxy_url_patterns has unexpected format: maven: Invalid type. Expected: array, given: string",
		"severity": "failure",
	}}
	assertions.assert_equal_results(expected_bad_patterns, sbom.deny) with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as d_bad_patterns

	# Invalid regex in allowed_proxy_url_patterns
	d_bad_regex := {
		"proxy_enabled_purl_types": [],
		"allowed_proxy_url_patterns": {"maven": ["(?=a)?b"]},
	}
	expected_bad_regex := {
		{
			"code": "sbom.disallowed_packages_provided",
			"msg": "\"(?=a)?b\" is not a valid regular expression for PURL type \"maven\"",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Pattern \"(?=a)?b\" for PURL type \"maven\" in allowed_proxy_url_patterns is not effectively anchored with ^",
			"severity": "warning",
		},
	}
	assertions.assert_equal_results(expected_bad_regex, sbom.deny) with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as d_bad_regex
}

test_vendored_purl_types_rule_data_validation if {
	# Valid data - no errors
	assertions.assert_empty(sbom.deny) with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as {"vendored_purl_types": ["golang", "cargo"]}

	# Invalid vendored_purl_types: wrong type and duplicate
	d_bad := {"vendored_purl_types": [1, "golang", "golang"]}
	expected_bad := {
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Rule data vendored_purl_types has unexpected format: 0: Invalid type. Expected: string, given: integer",
			"severity": "failure",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Rule data vendored_purl_types has unexpected format: (Root): array items[1,2] must be unique",
			"severity": "failure",
		},
	}
	assertions.assert_equal_results(expected_bad, sbom.deny) with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as d_bad
}

test_anchoring_warnings_external_references if {
	d := {
		lib.sbom.rule_data_allowed_external_references_key: [
			{"type": "distribution", "url": "^https://example\\.com/.*"},
			{"type": "distribution", "url": "example.com"},
		],
		lib.sbom.rule_data_disallowed_external_references_key: [
			{"type": "vcs", "url": "^https://evil\\.com/.*"},
			{"type": "vcs", "url": "evil.com"},
		],
	}

	expected := {
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Pattern \"example.com\" at index 1 in allowed_external_references is not effectively anchored with ^",
			"severity": "warning",
		},
		{
			"code": "sbom.disallowed_packages_provided",
			# regal ignore:line-length
			"msg": "Pattern \"evil.com\" at index 1 in disallowed_external_references is not effectively anchored with ^",
			"severity": "warning",
		},
	}

	assertions.assert_equal_results(expected, sbom.deny) with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as d
}

test_no_anchoring_warning_for_empty_or_absent_url if {
	d := {lib.sbom.rule_data_allowed_external_references_key: [
		{"type": "distribution", "url": "^https://valid\\.com/.*"},
		{"type": "distribution", "url": ""},
		{"type": "distribution"},
	]}

	# Empty or absent URL should not trigger anchoring warning
	warnings := {e | some e in sbom.deny; e.severity == "warning"; contains(e.msg, "anchored")}
	assertions.assert_empty(warnings) with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as d
}

test_anchoring_warnings_package_sources if {
	d := {lib.sbom.rule_data_allowed_package_sources_key: [
		{"type": "generic", "patterns": ["^https://registry\\.example\\.com/.*"]},
		{"type": "generic", "patterns": ["registry.example.com"]},
	]}

	expected := {{
		"code": "sbom.disallowed_packages_provided",
		# regal ignore:line-length
		"msg": "Pattern \"registry.example.com\" at index 1 in allowed_package_sources is not effectively anchored with ^",
		"severity": "warning",
	}}

	assertions.assert_equal_results(expected, sbom.deny) with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as d
}

test_anchoring_warnings_proxy_url_patterns if {
	d := {"allowed_proxy_url_patterns": {
		"maven": ["^https://proxy\\.example\\.com/.*"],
		"npm": ["proxy.example.com"],
	}}

	expected := {{
		"code": "sbom.disallowed_packages_provided",
		# regal ignore:line-length
		"msg": "Pattern \"proxy.example.com\" for PURL type \"npm\" in allowed_proxy_url_patterns is not effectively anchored with ^",
		"severity": "warning",
	}}

	assertions.assert_equal_results(expected, sbom.deny) with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as d
}

test_anchoring_warnings_except_when if {
	d := {lib.sbom.rule_data_attributes_key: [{
		"name": "test_attr",
		"value": "true",
		"except_when": [{"purl_qualifier": "repo", "patterns": ["console\\.redhat\\.com"]}],
	}]}

	expected := {{
		"code": "sbom.disallowed_packages_provided",
		# regal ignore:line-length
		"msg": "Pattern \"console\\\\.redhat\\\\.com\" at index 0 in disallowed_attributes except_when is not effectively anchored with ^",
		"severity": "warning",
	}}

	assertions.assert_equal_results(expected, sbom.deny) with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as d
}

test_no_anchoring_warnings_when_anchored if {
	d := {
		lib.sbom.rule_data_allowed_external_references_key: [{"type": "distribution", "url": "^https://example\\.com/.*"}],
		lib.sbom.rule_data_disallowed_external_references_key: [{"type": "vcs", "url": "^https://evil\\.com/.*"}],
		lib.sbom.rule_data_allowed_package_sources_key: [{"type": "generic", "patterns": ["^https://registry\\.example\\.com/.*"]}],
		"allowed_proxy_url_patterns": {"maven": ["^https://proxy\\.example\\.com/.*"]},
		lib.sbom.rule_data_attributes_key: [{
			"name": "test_attr",
			"value": "true",
			"except_when": [{"purl_qualifier": "repo", "patterns": ["^https://console\\.redhat\\.com/.*"]}],
		}],
	}

	assertions.assert_empty(sbom.deny) with input.attestations as _sbom_attestation
		with lib.sbom._verified_sbom_attestations as _sbom_attestation
		with data.rule_data as d
}

test_sbom_signature_verification_warn if {
	mock_referrers := [{
		"mediaType": "application/vnd.oci.image.manifest.v1+json",
		"size": 100,
		# regal ignore:line-length
		"digest": "sha256:a1b2c3d400000000000000000000000000000000000000000000000a1b2c3d4",
		"artifactType": "application/vnd.cyclonedx+json",
		# regal ignore:line-length
		"ref": "registry.io/repo/img@sha256:a1b2c3d400000000000000000000000000000000000000000000000a1b2c3d4",
	}]
	expected := {{
		"code": "sbom.signature_verification",
		# regal ignore:line-length
		"msg": "SBOM referrer signature verification failed for registry.io/repo/img@sha256:a1b2c3d400000000000000000000000000000000000000000000000a1b2c3d4: verification failed",
	}}
	assertions.assert_equal_results(expected, sbom.warn) with input.attestations as []
		with lib.sbom._verified_sbom_attestations as []
		with input.image.ref as "registry.io/repo/img@sha256:abc123"
		with ec.oci.image_referrers as mock_referrers
		with ec.oci.image_tag_refs as []
		with ec.sigstore.verify_attestation as _mock_verify_attestation_success
		with ec.sigstore.verify_image as _mock_verify_image_failure
		with data.rule_data__configuration__ as {"signing_identities": {"sbom": {"public_key": "test-key", "ignore_rekor": true}}}
}

test_sbom_signature_verification_no_warn_on_success if {
	mock_referrers := [{
		"mediaType": "application/vnd.oci.image.manifest.v1+json",
		"size": 100,
		# regal ignore:line-length
		"digest": "sha256:a1b2c3d400000000000000000000000000000000000000000000000a1b2c3d4",
		"artifactType": "application/vnd.cyclonedx+json",
		# regal ignore:line-length
		"ref": "registry.io/repo/img@sha256:a1b2c3d400000000000000000000000000000000000000000000000a1b2c3d4",
	}]
	assertions.assert_empty(sbom.warn) with input.attestations as []
		with lib.sbom._verified_sbom_attestations as []
		with input.image.ref as "registry.io/repo/img@sha256:abc123"
		with ec.oci.image_referrers as mock_referrers
		with ec.oci.image_tag_refs as []
		with ec.sigstore.verify_attestation as _mock_verify_attestation_success
		with ec.sigstore.verify_image as _mock_verify_image_success
		with data.rule_data__configuration__ as {"signing_identities": {"sbom": {"public_key": "test-key", "ignore_rekor": true}}}
}

test_sbom_signature_verification_no_warn_without_identity if {
	assertions.assert_empty(sbom.warn) with input.attestations as []
		with lib.sbom._verified_sbom_attestations as []
		with input.image.ref as "registry.io/repo/img@sha256:abc123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
}

test_sbom_attestation_signature_verification_warn if {
	expected := {{
		"code": "sbom.signature_verification",
		"msg": "SBOM attestation signature verification failed for registry.io/repo/img@sha256:abc123: verification failed",
	}}
	assertions.assert_equal_results(expected, sbom.warn) with input.attestations as []
		with lib.sbom._verified_sbom_attestations as []
		with input.image.ref as "registry.io/repo/img@sha256:abc123"
		with ec.oci.image_referrers as []
		with ec.oci.image_tag_refs as []
		with ec.sigstore.verify_attestation as _mock_verify_attestation_failure
		with data.rule_data__configuration__ as {"signing_identities": {"sbom": {"public_key": "test-key", "ignore_rekor": true}}}
}

_mock_verify_attestation_success(_, _) := {"success": true, "errors": [], "attestations": []}

_mock_verify_attestation_failure(_, _) := {"success": false, "errors": ["verification failed"], "attestations": []}

_mock_verify_image_success(_, _) := {"errors": []}

_mock_verify_image_failure(_, _) := {"errors": ["verification failed"]}

_sbom_attestation := [_spdx_sbom_attestation, _cyclonedx_sbom_attestation]

_spdx_sbom_attestation := {"statement": {
	"predicateType": "https://spdx.dev/Document",
	"predicate": {
		"spdxVersion": "SPDX-2.3",
		"documentNamespace": "https://example.dev/spdxdocs/example-310683af-e9a0-4f66-a6a4-119352915b51",
		"dataLicense": "CC0-1.0",
		"SPDXID": "SPDXRef-DOCUMENT",
		"name": "registry.local/bacon@sha256:123",
		"creationInfo": {
			"created": "2006-08-14T02:34:56-06:00",
			"creators": ["Tool: example SPDX document only"],
		},
		"packages": [{
			"SPDXID": "SPDXRef-image-index",
			"name": "spam",
			"versionInfo": "1.1.2-25",
			"supplier": "Organization: Red Hat",
			"downloadLocation": "NOASSERTION",
			"licenseDeclared": "Apache-2.0",
			"externalRefs": [{
				"referenceCategory": "PACKAGE-MANAGER",
				"referenceType": "purl",
				# regal ignore:line-length
				"referenceLocator": "pkg:oci/kernel-module-management-rhel9-operator@sha256%3Ad845f0bd93dad56c92c47e8c116a11a0cc5924c0b99aed912b4f8b54178efa98",
			}],
			"checksums": [{
				"algorithm": "SHA256",
				"checksumValue": "d845f0bd93dad56c92c47e8c116a11a0cc5924c0b99aed912b4f8b54178efa98",
			}],
		}],
		"files": [{
			"fileName": "/usr/bin/spam",
			"SPDXID": "SPDXRef-File-usr-bin-spam-0e18b4ee77321ba5",
			"checksums": [{
				"algorithm": "SHA256",
				"checksumValue": "d845f0bd93dad56c92c47e8c116a11a0cc5924c0b99aed912b4f8b54178efa98",
			}],
		}],
	},
}}

_cyclonedx_sbom_attestation := {"statement": {
	"predicateType": "https://cyclonedx.org/bom",
	"predicate": {
		"$schema": "http://cyclonedx.org/schema/bom-1.5.schema.json",
		"bomFormat": "CycloneDX",
		"specVersion": "1.5",
		"serialNumber": "urn:uuid:cf1a2c3d-bcf8-45c4-9d0f-b2b59a0753f0",
		"version": 1,
		"metadata": {
			"timestamp": "2023-11-20T17:32:41Z",
			"tools": [{
				"vendor": "anchore",
				"name": "syft",
				"version": "0.96.0",
			}],
			"component": {
				"bom-ref": "158c8a990fbd4038",
				"type": "file",
				"name": "/var/lib/containers/storage/vfs/dir/dfd74fe178f4ea0472b5569bff38a4df69d05e7a81b538c98d731566aec15a69",
			},
		},
		"components": [{
			# regal ignore:line-length
			"bom-ref": "pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3&package-id=f4f4e3cc2a6d9c37",
			"type": "library",
			"publisher": "Red Hat, Inc.",
			"name": "coreutils-single",
			"version": "8.32-34.el9",
			"licenses": [{"license": {"name": "GPLv3+"}}],
			"cpe": "cpe:2.3:a:coreutils-single:coreutils-single:8.32-34.el9:*:*:*:*:*:*:*",
			# regal ignore:line-length
			"purl": "pkg:rpm/rhel/coreutils-single@8.32-34.el9?arch=x86_64&upstream=coreutils-8.32-34.el9.src.rpm&distro=rhel-9.3",
			"properties": [
				{"name": "attr1"},
				{
					"name": "attr2",
					"value": "value2",
				},
			],
			"externalReferences": [{
				"type": "distribution",
				"url": "https://example.com/file.txt",
			}],
		}],
	},
}}
